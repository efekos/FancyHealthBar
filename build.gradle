plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.15'
}

def loadProps(path) {
    def p = new Properties()
    file(path).withInputStream { p.load(it) }
    p.each { k, v -> project.ext.set(k, v) }
}

loadProps("../../environment.properties")

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}
def targetJavaVersion = stonecutter.eval(stonecutter.current.version,">=1.20.5")?21:17

modrinth {
    token = project.modrinth_token
    projectId = "d5wz0u1I"
    versionNumber = project.version+"+"+stonecutter.current.version
    versionType="release"
    uploadFile=remapJar
    gameVersions=[stonecutter.current.version]
    loaders=["fabric"]
    changelog=file("../../changelog.txt").text
    detectLoaders = false
    dependencies {
        optional.project "modmenu"
    }
}


task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = project.curseforge_token

    def mainFile =  upload(990963,remapJar)
    mainFile.changelog = file("../../changelog.txt").text
    mainFile.addOptional('modmenu')
    mainFile.releaseType = 'release'
    mainFile.changelogType = 'markdown'
    mainFile.addGameVersion(stonecutter.current.version,'Fabric',"Java $targetJavaVersion",'Client')
}


repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu}"
}

def aw_file = "fancyhealthbar."+stonecutter.current.version+".accesswidener"

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", stonecutter.current.version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": stonecutter.current.version,
                "loader_version": project.loader_version,
                "aw_file": aw_file
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

remapJar {
    archiveFileName ="FancyHealthBar-"+stonecutter.current.version+"-"+project.version+".jar";
}

loom {
    runConfigs.all {
        ideConfigGenerated true
        runDir "../../run"
    }

    //?<1.21.11{
    /*
    mixin {
        defaultRefmapName = "fancyhealthbar.refmap.json"
    }*/
    //?}

    accessWidenerPath = parent.file("src/main/resources/accessWidener/$aw_file")
}
